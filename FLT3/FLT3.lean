/-
Copyright (c) 2024 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca, Sanyam Gupta, Omar Haddad, David Lowry-Duda,
  Lorenzo Luccioli, Pietro Monticone, Alexis Saurin, Florent Schaffhauser
-/
import Mathlib.NumberTheory.Cyclotomic.PID
import Mathlib.NumberTheory.FLT.Basic
import Mathlib.NumberTheory.FLT.Three
import FLT3.Cyclo

/-!
# Fermat's Last Theorem for Exponent `n = 3`
The goal of this file is to prove Fermat's Last Theorem for exponent `3`, namely that
for all `a`, `b`, `c` in `‚Ñï` such that `a ‚â† 0` and `b ‚â† 0` and `c ‚â† 0`,
then `a ^ 3 + b ^ 3 ‚â† c ^ 3`.
-/

open NumberField nonZeroDivisors IsCyclotomicExtension.Rat.Three

section case2

section eisenstein

attribute [instance] IsCyclotomicExtension.Rat.three_pid

variable {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {3} ‚Ñö K]

attribute [-instance] ValuationRing.instIsBezout -- This seems to be a very slow instance

-- This takes Lean some time, so let's do it here once
local instance : IsBezout (ùìû K) := by infer_instance

noncomputable
instance : NormalizedGCDMonoid (ùìû K) :=
  have : Nonempty (NormalizedGCDMonoid (ùìû K)) := inferInstance
  this.some

variable {Œ∂ : K} (hŒ∂ : IsPrimitiveRoot Œ∂ (3 : ‚Ñï+))

local notation3 "Œ∑" => hŒ∂.toInteger
local notation3 "Œª" => Œ∑ - 1

section Solution'

/-- `Solution'` is a tuple given by a solution to `a ^ 3 + b ^ 3 = u * c ^ 3`,
where `a`, `b`, `c` and `u` are as above. See `Solution` for the actual structure on which we will
do the descent. -/
structure Solution' where
  (a : ùìû K)
  (b : ùìû K)
  (c : ùìû K)
  (u : (ùìû K)À£)
  (ha : ¬¨ Œª ‚à£ a)
  (hb : ¬¨ Œª ‚à£ b)
  (hc : c ‚â† 0)
  (coprime : IsCoprime a b)
  (hcdvd : Œª ‚à£ c)
  (H : a ^ 3 + b ^ 3 = u * c ^ 3)

/-- `Solution` is the same as `Solution'` with the additional assumption that `Œª ^ 2 ‚à£ a + b`. -/
structure Solution extends Solution' hŒ∂ where
  (hab : Œª ^ 2 ‚à£ a + b)

variable {hŒ∂} (S : Solution hŒ∂) (S' : Solution' hŒ∂) [DecidableRel fun (a b : ùìû K) ‚Ü¶ a ‚à£ b]

/-- For any `S' : Solution'`, the multiplicity of `Œª` in `S'.c` is finite. -/
lemma Solution'.multiplicity_lambda_c_finite :
    multiplicity.Finite (hŒ∂.toInteger - 1) S'.c :=
  multiplicity.finite_of_not_isUnit (lambda_not_unit hŒ∂) S'.hc

/-- Given `S' : Solution'`, `S'.multiplicity` is the multiplicity of `Œª` in `S'.c`, as a natural
number. -/
def Solution'.multiplicity  :=
  (_root_.multiplicity (hŒ∂.toInteger - 1) S'.c).get (multiplicity_lambda_c_finite S')

/-- Given `S : Solution`, `S.multiplicity` is the multiplicity of `Œª` in `S.c`, as a natural
number. -/
def Solution.multiplicity := S.toSolution'.multiplicity

/-- We say that `S : Solution` is minimal if for all `S‚ÇÅ : Solution`, the multiplicity of `Œª` in
`S.c` is less or equal than the multiplicity in `S'.c`. -/
def Solution.isMinimal : Prop := ‚àÄ (S‚ÇÅ : Solution hŒ∂), S.multiplicity ‚â§ S‚ÇÅ.multiplicity

/-- If there is a solution then there is a minimal one. -/
lemma Solution.exists_minimal : ‚àÉ (S‚ÇÅ : Solution hŒ∂), S‚ÇÅ.isMinimal := by
  classical
  let T : Set ‚Ñï := { n | ‚àÉ (S' : Solution hŒ∂), S'.multiplicity = n }
  rcases Nat.find_spec (‚ü®S.multiplicity, ‚ü®S, rfl‚ü©‚ü© : T.Nonempty) with ‚ü®S‚ÇÅ, hS‚ÇÅ‚ü©
  exact ‚ü®S‚ÇÅ, fun S'' ‚Ü¶ hS‚ÇÅ ‚ñ∏ Nat.find_min' _ ‚ü®S'', rfl‚ü©‚ü©

end Solution'

section FermatLastTheoremForThreeGen

section Solution'

variable {hŒ∂} (S : Solution' hŒ∂)

/-- Given `S : Solution'`, then `S.a` and `S.b` are both congruent to `1` modulo `Œª ^ 4` or are
both congruent to `-1`.  -/
lemma a_cube_b_cube_same_congr :
    Œª ^ 4 ‚à£ S.a ^ 3 - 1 ‚àß Œª ^ 4 ‚à£ S.b ^ 3 + 1 ‚à®  Œª ^ 4 ‚à£ S.a ^ 3 + 1 ‚àß Œª ^ 4 ‚à£ S.b ^ 3 - 1 := by
  obtain ‚ü®z, hz‚ü© := S.hcdvd
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hŒ∂ S.ha with
    (‚ü®x, hx‚ü© | ‚ü®x, hx‚ü©) <;>
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hŒ∂ S.hb with
    (‚ü®y, hy‚ü© | ‚ü®y, hy‚ü©)
  ¬∑ exfalso
    refine lambda_not_dvd_two hŒ∂ ‚ü®S.u * Œª ^ 2 * z ^ 3 - Œª ^ 3 * (x + y), ?_‚ü©
    symm
    calc _ = S.u * (Œª * z) ^ 3 - Œª ^ 4 * x - Œª ^ 4 * y := by ring
    _ = (S.a ^ 3 + S.b ^ 3) - (S.a ^ 3 - 1) - (S.b ^ 3 - 1) := by rw [‚Üê hx, ‚Üê hy, ‚Üê hz, ‚Üê S.H]
    _ = 2 := by ring
  ¬∑ left
    exact ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©
  ¬∑ right
    exact ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©
  ¬∑ exfalso
    refine lambda_not_dvd_two hŒ∂ ‚ü®Œª ^ 3 * (x + y) - S.u * Œª ^ 2 * z ^ 3, ?_‚ü©
    symm
    calc _ =  Œª ^ 4 * x + Œª ^ 4 * y - S.u * (Œª * z) ^ 3 := by ring
    _ = (S.a ^ 3 + 1) + (S.b ^ 3 + 1) - (S.a ^ 3 + S.b ^ 3) := by rw [‚Üê hx, ‚Üê hy, ‚Üê hz, ‚Üê S.H]
    _ = 2 := by ring

/-- Given `S : Solution'`, we have that `Œª ^ 4` divides `S.c ^ 3`. -/
lemma lambda_pow_four_dvd_c_cube : Œª ^ 4 ‚à£ S.c ^ 3 := by
  rcases a_cube_b_cube_same_congr S with
    (‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü© | ‚ü®‚ü®x, hx‚ü©, ‚ü®y, hy‚ü©‚ü©) <;> {
  refine ‚ü®S.u‚Åª¬π * (x + y), ?_‚ü©
  symm
  calc _ = S.u‚Åª¬π * (Œª ^ 4 * x + Œª ^ 4 * y) := by ring
  _ = S.u‚Åª¬π * (S.a ^ 3 + S.b ^ 3) := by rw [‚Üê hx, ‚Üê hy]; ring
  _ = S.u‚Åª¬π * (S.u * S.c ^ 3) := by rw [S.H]
  _ = S.c ^ 3 := by simp }

/-- Given `S : Solution'`, we have that `Œª ^ 2` divides `S.c`. -/
lemma lambda_pow_two_dvd_c : Œª ^ 2 ‚à£ S.c := by
  classical
  have  hm := S.multiplicity_lambda_c_finite
  suffices 2 ‚â§ (multiplicity ((hŒ∂.toInteger - 1)) S.c).get hm by
    ¬∑ obtain ‚ü®x, hx‚ü© := multiplicity.pow_multiplicity_dvd hm
      refine ‚ü®Œª ^ ((multiplicity ((hŒ∂.toInteger - 1)) S.c).get hm - 2) * x, ?_‚ü©
      rw [‚Üê mul_assoc, ‚Üê pow_add]
      convert hx using 3
      simp [this]
  have := lambda_pow_four_dvd_c_cube S
  have hm1 :(multiplicity (hŒ∂.toInteger - 1) (S.c ^ 3)).get
    (multiplicity.finite_pow hŒ∂.lambda_prime hm) =
    multiplicity (hŒ∂.toInteger - 1) (S.c ^ 3) := by simp
  rw [multiplicity.pow_dvd_iff_le_multiplicity, ‚Üê hm1, multiplicity.pow' hŒ∂.lambda_prime hm,
    Nat.cast_ofNat, Nat.ofNat_le_cast] at this
  linarith

variable [DecidableRel fun (a b : ùìû K) ‚Ü¶ a ‚à£ b]

/-- Given `S : Solution'`, we have that `2 ‚â§ S.multiplicity`. -/
lemma Solution'.two_le_multiplicity : 2 ‚â§ S.multiplicity := by
  simpa [‚Üê PartENat.coe_le_coe, Solution'.multiplicity] using
    multiplicity.le_multiplicity_of_pow_dvd (lambda_pow_two_dvd_c S)

/-- Given `S : Solution`, we have that `2 ‚â§ S.multiplicity`. -/
lemma Solution.two_le_multiplicity (S : Solution hŒ∂) : 2 ‚â§ S.multiplicity := by
  exact S.toSolution'.two_le_multiplicity

/-- Given `S : Solution'`, the key factorization of `S.a ^ 3 + S.b ^ 3`. -/
lemma cube_add_cube_eq_mul :
    S.a ^ 3 + S.b ^ 3 = (S.a + S.b) * (S.a + Œ∑ * S.b) * (S.a + Œ∑ ^ 2 * S.b) := by
  symm
  calc _ = S.a^3+S.a^2*S.b*(Œ∑^2+Œ∑+1)+S.a*S.b^2*(Œ∑^2+Œ∑+Œ∑^3)+Œ∑^3*S.b^3 := by ring
  _ = S.a^3+S.a^2*S.b*(Œ∑^2+Œ∑+1)+S.a*S.b^2*(Œ∑^2+Œ∑+1)+S.b^3 :=
    by rw [hŒ∂.toInteger_cube_eq_one, one_mul]
  _ = S.a ^ 3 + S.b ^ 3 := by rw [hŒ∂.toInteger_eval_cyclo]; ring

open PartENat in

/-- Given `S : Solution'`, we have that `Œª ^ 2` divides one amongst `S.a + S.b`,
`S.a + Œ∑ * S.b` and `S.a + Œ∑ ^ 2 * S.b`. -/
lemma lambda_sq_dvd_or_dvd_or_dvd :
    Œª ^ 2 ‚à£ S.a + S.b ‚à® Œª ^ 2 ‚à£ S.a + Œ∑ * S.b ‚à® Œª ^ 2 ‚à£ S.a + Œ∑ ^ 2 * S.b := by
  classical
  by_contra! h
  rcases h with ‚ü®h1, h2, h3‚ü©
  rw [‚Üê multiplicity.multiplicity_lt_iff_not_dvd] at h1 h2 h3
  have h1' : multiplicity.Finite (hŒ∂.toInteger - 1) (S.a + S.b) :=
    multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ by simp [ht] at h1)
  have h2' : multiplicity.Finite (hŒ∂.toInteger - 1) (S.a + Œ∑ * S.b) :=
    multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ by simp [ht] at h2)
  have h3' : multiplicity.Finite (hŒ∂.toInteger - 1) (S.a + Œ∑ ^ 2 * S.b) :=
    multiplicity.ne_top_iff_finite.1 (fun ht ‚Ü¶ by simp [ht] at h3)
  replace h1' : (multiplicity (hŒ∂.toInteger - 1) (S.a + S.b)).get h1' =
    multiplicity (hŒ∂.toInteger - 1) (S.a + S.b) := by simp
  replace h2' : (multiplicity (hŒ∂.toInteger - 1) (S.a + Œ∑ * S.b)).get h2' =
    multiplicity (hŒ∂.toInteger - 1) (S.a + Œ∑ * S.b) := by simp
  replace h3' : (multiplicity (hŒ∂.toInteger - 1) (S.a + Œ∑ ^ 2 * S.b)).get h3' =
    multiplicity (hŒ∂.toInteger - 1) (S.a + Œ∑ ^ 2 * S.b) := by simp
  rw [‚Üê h1', coe_lt_coe] at h1; rw [‚Üê h2', coe_lt_coe] at h2; rw [‚Üê h3', coe_lt_coe] at h3
  have := (pow_dvd_pow_of_dvd (lambda_pow_two_dvd_c S) 3).mul_left S.u
  rw [‚Üê pow_mul, ‚Üê S.H, cube_add_cube_eq_mul, multiplicity.pow_dvd_iff_le_multiplicity,
    multiplicity.mul hŒ∂.zeta_sub_one_prime', multiplicity.mul (IsPrimitiveRoot.lambda_prime hŒ∂), ‚Üê h1', ‚Üê h2',
    ‚Üê h3', ‚Üê Nat.cast_add, ‚Üê Nat.cast_add, coe_le_coe] at this
  omega

/-- Given `S : Solution'`, we may assume that `Œª ^ 2` divides `S.a + S.b ‚à® Œª ^ 2` (see also the
result below). -/
lemma ex_dvd_a_add_b : ‚àÉ (a' b' : ùìû K), a' ^ 3 + b' ^ 3 = S.u * S.c ^ 3 ‚àß
    IsCoprime a' b' ‚àß ¬¨ Œª ‚à£ a' ‚àß ¬¨ Œª ‚à£ b' ‚àß Œª ^ 2 ‚à£ a' + b' := by
  rcases lambda_sq_dvd_or_dvd_or_dvd S with (h | h | h)
  ¬∑ exact ‚ü®S.a, S.b, S.H, S.coprime, S.ha, S.hb, h‚ü©
  ¬∑ refine ‚ü®S.a, Œ∑ * S.b, ?_, ?_, S.ha, fun ‚ü®x, hx‚ü© ‚Ü¶ S.hb ‚ü®Œ∑ ^ 2 * x, ?_‚ü©, h‚ü©
    ¬∑ rw [mul_pow, hŒ∂.toInteger_cube_eq_one, one_mul, S.H]
    ¬∑ exact (isCoprime_mul_unit_left_right hŒ∂.eta_isUnit _ _).2 S.coprime
    ¬∑ rw [mul_comm _ x, ‚Üê mul_assoc, ‚Üê hx, mul_comm _ S.b, mul_assoc, ‚Üê pow_succ',
        hŒ∂.toInteger_cube_eq_one, mul_one]
  ¬∑ refine ‚ü®S.a, Œ∑ ^ 2 * S.b, ?_, ?_, S.ha, fun ‚ü®x, hx‚ü© ‚Ü¶ S.hb ‚ü®Œ∑ * x, ?_‚ü©, h‚ü©
    ¬∑ rw [mul_pow, ‚Üê pow_mul, mul_comm 2, pow_mul, hŒ∂.toInteger_cube_eq_one, one_pow, one_mul, S.H]
    ¬∑ exact (isCoprime_mul_unit_left_right (hŒ∂.eta_isUnit.pow _) _ _).2 S.coprime
    ¬∑ rw [mul_comm _ x, ‚Üê mul_assoc, ‚Üê hx, mul_comm _ S.b, mul_assoc, ‚Üê pow_succ,
        hŒ∂.toInteger_cube_eq_one, mul_one]

/-- Given `S : Solution'`, then there is `S‚ÇÅ : Solution` such that
`S‚ÇÅ.multiplicity = S.multiplicity`. -/
lemma exists_Solution_of_Solution' : ‚àÉ (S‚ÇÅ : Solution hŒ∂), S‚ÇÅ.multiplicity = S.multiplicity := by
  obtain ‚ü®a, b, H, coprime, ha, hb, hab‚ü© := ex_dvd_a_add_b S
  exact ‚ü®
  { a := a
    b := b
    c := S.c
    u := S.u
    ha := ha
    hb := hb
    hc := S.hc
    coprime := coprime
    hcdvd := S.hcdvd
    H := H
    hab := hab }, rfl‚ü©

end Solution'

namespace Solution

variable (S : Solution hŒ∂) {hŒ∂}

lemma a_add_eta_b : S.a + Œ∑ * S.b = (S.a + S.b) + Œª * S.b := by ring

/-- Given `(S : Solution)`, we have that `Œª ‚à£ (S.a + Œ∑ * S.b)`. -/
lemma lambda_dvd_a_add_eta_mul_b : Œª ‚à£ (S.a + Œ∑ * S.b) := by
  rw [a_add_eta_b]
  exact dvd_add (dvd_trans (dvd_pow_self _ (by decide)) S.hab) ‚ü®S.b, by rw [mul_comm]‚ü©

/-- Given `(S : Solution)`, we have that `Œª ‚à£ (S.a + Œ∑ ^ 2 * S.b)`. -/
lemma lambda_dvd_a_add_eta_sq_mul_b : Œª ‚à£ (S.a + Œ∑ ^ 2 * S.b) := by
  rw [show S.a + Œ∑ ^ 2 * S.b = (S.a + S.b) + Œª ^ 2 * S.b + 2 * Œª * S.b by ring]
  exact dvd_add (dvd_add (dvd_trans (dvd_pow_self _ (by decide)) S.hab) ‚ü®Œª * S.b, by ring‚ü©)
    ‚ü®2 * S.b, by ring‚ü©

/-- Given `(S : Solution)`, we have that `Œª ^ 2` does not divide `S.a + Œ∑ * S.b`. -/
lemma lambda_sq_not_a_add_eta_mul_b : ¬¨ Œª ^ 2 ‚à£ (S.a + Œ∑ * S.b) := by
  simp_rw [a_add_eta_b, dvd_add_right S.hab, pow_two, mul_dvd_mul_iff_left (lambda_ne_zero hŒ∂), S.hb,
    not_false_eq_true]

/-- Given `(S : Solution)`, we have that `Œª ^ 2` does not divide `S.a + Œ∑ ^ 2 * S.b`. -/
lemma lambda_sq_not_dvd_a_add_eta_sq_mul_b : ¬¨ Œª ^ 2 ‚à£ (S.a + Œ∑ ^ 2 * S.b) := by
  intro h
  apply S.hb
  rcases h with ‚ü®k, hk‚ü©
  rw [show S.a + Œ∑ ^ 2 * S.b = S.a + S.b - S.b + Œ∑ ^ 2 * S.b by ring] at hk
  rcases S.hab with ‚ü®k', hk'‚ü©
  use (k - k') * (-Œ∑)
  rw [hk'] at hk
  rw [show Œª ^ 2 * k' - S.b + Œ∑ ^ 2 * S.b = Œª * (S.b * (Œ∑ +1) + Œª * k') by ring, pow_two, mul_assoc] at hk
  simp only [mul_eq_mul_left_iff, lambda_ne_zero, or_false] at hk
  replace hk := congr_arg (fun x => x * (-Œ∑)) hk
  simp only at hk
  rw [show (S.b * (Œ∑ + 1) + Œª * k') * -Œ∑ = (- S.b) * (Œ∑ ^ 2 + Œ∑ + 1 - 1) - Œ∑ * Œª * k' by ring] at hk
  rw [hŒ∂.toInteger_eval_cyclo] at hk
  simp only [zero_sub, mul_neg, mul_one, neg_neg] at hk
  rw [sub_eq_iff_eq_add] at hk
  rw [hk]
  ring

lemma eta_add_one_inv : (Œ∑ + 1) * (-Œ∑) = 1 := by
  calc (Œ∑ + 1) * -Œ∑ = -(Œ∑ ^ 2 + Œ∑ + 1) + 1  := by ring
  _ = 1 := by rw [hŒ∂.toInteger_eval_cyclo]; simp

/-- If `p : ùìû K` is a prime that divides both `S.a + S.b` and `S.a + Œ∑ * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_b_of_dvd_a_add_eta_mul_b {p : ùìû K} (hp : Prime p)
    (hpab : p ‚à£ S.a + S.b) (hpaetab : p ‚à£ S.a + Œ∑ * S.b) : Associated p Œª := by
  by_cases p_lam : (p ‚à£ Œª)
  ¬∑ exact hp.associated_of_dvd hŒ∂.lambda_prime p_lam
  have pdivb : p ‚à£ S.b := by
    have fgh : p ‚à£ (Œª * S.b) := by
      rw [show Œª * S.b = (S.a + Œ∑ * S.b) - (S.a + S.b) by ring]
      exact dvd_sub hpaetab hpab
    rcases Prime.dvd_or_dvd hp fgh with (h | h)
    ¬∑ contradiction
    ¬∑ exact h
  have pdiva : p ‚à£ S.a := by
    have fgh : p ‚à£ (Œª * S.a) := by
      rw [show Œª * S.a = Œ∑ * (S.a + S.b) - (S.a + Œ∑ * S.b) by ring]
      exact dvd_sub (dvd_mul_of_dvd_right hpab _) hpaetab
    rcases Prime.dvd_or_dvd hp fgh with (h | h)
    ¬∑ tauto
    ¬∑ exact h
  have punit := IsCoprime.isUnit_of_dvd' S.coprime pdiva pdivb
  exfalso
  exact hp.not_unit punit

/-- If `p : ùìû K` is a prime that divides both `S.a + S.b` and `S.a + Œ∑ ^ 2 * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_b_of_dvd_a_add_eta_sq_mul_b {p : ùìû K} (hp : Prime p)
  (hpab : p ‚à£ (S.a + S.b)) (hpaetasqb : p ‚à£ (S.a + Œ∑ ^ 2 * S.b)) : Associated p Œª := by
  by_cases p_lam : (p ‚à£ Œª)
  ¬∑ exact hp.associated_of_dvd hŒ∂.lambda_prime p_lam
  have pdivb : p ‚à£ S.b := by
    have fgh : p ‚à£ Œª * S.b := by
      rw [show Œª * S.b = - (1 - Œ∑) * S.b by ring, ‚Üê hŒ∂.toInteger_cube_eq_one]
      rw [show - (Œ∑ ^ 3 - Œ∑) * S.b = Œ∑ * ((S.a + S.b) - (S.a + Œ∑ ^ 2 * S.b)) by ring]
      rw [hŒ∂.eta_isUnit.dvd_mul_left]
      exact hpab.sub hpaetasqb
    exact hp.dvd_or_dvd fgh |>.resolve_left p_lam
  have pdiva : p ‚à£ S.a := by
    have fgh : p ‚à£ Œª * S.a := by
      rw [show Œª * S.a = - (1 - Œ∑) * S.a by ring, ‚Üê hŒ∂.toInteger_cube_eq_one]
      rw [show - (Œ∑ ^ 3 - Œ∑) * S.a = Œ∑ * ((S.a + Œ∑ ^ 2 * S.b) - Œ∑ ^ 2 * (S.a + S.b)) by ring]
      rw [hŒ∂.eta_isUnit.dvd_mul_left]
      exact hpaetasqb.sub (dvd_mul_of_dvd_right hpab _)
    exact hp.dvd_or_dvd fgh |>.resolve_left p_lam
  have punit := S.coprime.isUnit_of_dvd' pdiva pdivb
  exact hp.not_unit punit |>.elim

/-- If `p : ùìû K` is a prime that divides both `S.a + Œ∑ * S.b` and `S.a + Œ∑ ^ 2 * S.b`, then `p`
is associated with `Œª`. -/
lemma associated_of_dvd_a_add_eta_mul_b_of_dvd_a_add_eta_sq_mul_b {p : ùìû K} (hp : Prime p)
    (hpaetab : p ‚à£ S.a + Œ∑ * S.b) (hpaetasqb : p ‚à£ S.a + Œ∑ ^ 2 * S.b) : Associated p Œª := by
  by_cases p_lam : (p ‚à£ Œª)
  ¬∑ exact hp.associated_of_dvd hŒ∂.lambda_prime p_lam
  have pdivb : p ‚à£ S.b := by
    have fgh : p ‚à£ Œ∑ * (Œª * S.b) := by
      rw [show Œ∑ * (Œª * S.b) = (S.a + Œ∑ ^ 2 * S.b) - (S.a + Œ∑ * S.b) by ring]
      exact hpaetasqb.sub hpaetab
    rw [hŒ∂.eta_isUnit.dvd_mul_left] at fgh
    exact hp.dvd_or_dvd fgh |>.resolve_left p_lam
  have pdiva : p ‚à£ S.a := by
    have fgh : p ‚à£ Œª * S.a := by
      rw [show Œª * S.a = Œ∑ * (S.a + Œ∑ * S.b) - (S.a + Œ∑ ^ 2 * S.b) by ring]
      exact dvd_mul_of_dvd_right hpaetab _ |>.sub hpaetasqb
    exact hp.dvd_or_dvd fgh |>.resolve_left p_lam
  have punit := S.coprime.isUnit_of_dvd' pdiva pdivb
  exact hp.not_unit punit |>.elim

/-- Given `S : Solution`, we let `S.y` be the element such that
`S.a + Œ∑ * S.b = Œª * S.y` -/
noncomputable
def y := (lambda_dvd_a_add_eta_mul_b S).choose

lemma y_spec : S.a + Œ∑ * S.b = Œª * S.y :=
  (lambda_dvd_a_add_eta_mul_b S).choose_spec

/-- Given `S : Solution`, we let `S.z` be the element such that
`S.a + Œ∑ ^ 2 * S.b = Œª * S.z` -/
noncomputable
def z := (lambda_dvd_a_add_eta_sq_mul_b S).choose

lemma z_spec : S.a + Œ∑ ^ 2 * S.b = Œª * S.z :=
  (lambda_dvd_a_add_eta_sq_mul_b S).choose_spec

lemma lambda_not_dvd_y : ¬¨ Œª ‚à£ S.y := by
  intro h
  replace h := mul_dvd_mul_left (Œ∑ - 1) h
  rw [‚Üê y_spec] at h
  rw [‚Üê pow_two] at h
  exact lambda_sq_not_a_add_eta_mul_b _ h

lemma lambda_not_dvd_z : ¬¨ Œª ‚à£ S.z := by
  intro h
  replace h := mul_dvd_mul_left (Œ∑ - 1) h
  rw [‚Üê z_spec] at h
  rw [‚Üê pow_two] at h
  exact lambda_sq_not_dvd_a_add_eta_sq_mul_b _ h

variable [DecidableRel fun (a b : ùìû K) ‚Ü¶ a ‚à£ b]

/-- We have that `Œª ^ (3*S.multiplicity-2)` divides `S.a + S.b`. -/
lemma lambda_pow_dvd_a_add_b : Œª ^ (3 * S.multiplicity - 2) ‚à£ S.a + S.b := by
  have h : Œª ^ S.multiplicity ‚à£ S.c  := multiplicity.pow_multiplicity_dvd _
  replace h := pow_dvd_pow_of_dvd h 3
  replace h : (Œª ^ multiplicity S) ^ 3 ‚à£ S.u * S.c ^ 3 := by simp [h]
  rw [‚Üê S.H, cube_add_cube_eq_mul, ‚Üê pow_mul, mul_comm] at h
  apply hŒ∂.lambda_prime.pow_dvd_of_dvd_mul_left _ S.lambda_not_dvd_z
  apply hŒ∂.lambda_prime.pow_dvd_of_dvd_mul_left _ S.lambda_not_dvd_y
  rw [y_spec, z_spec] at h
  have := S.two_le_multiplicity
  have hh : 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S := by
    omega
  rw [‚Üê hh, pow_succ, pow_succ] at h
  rw [show (S.a + S.b) * (Œª * y S) * (Œª * z S) = (S.a + S.b) * y S * z S * Œª * Œª by ring] at h
  simp only [mul_dvd_mul_iff_right (lambda_ne_zero hŒ∂)] at h
  rwa [show (S.a + S.b) * y S * z S = y S * (z S * (S.a + S.b)) by ring] at h

/-- Given `S : Solution`, we let `S.x` be the element such that
`S.a + S.b = Œª ^ (3*S.multiplicity-2) * S.x` -/
noncomputable
def x := (lambda_pow_dvd_a_add_b S).choose

lemma x_spec : S.a + S.b = Œª ^ (3*S.multiplicity-2) * S.x :=
  (lambda_pow_dvd_a_add_b S).choose_spec


/-- Given `S : Solution`, we let `S.w` be the element such that
`S.c = Œª ^ S.multiplicity * S.w` -/
noncomputable
def w :=
  (multiplicity.pow_multiplicity_dvd S.toSolution'.multiplicity_lambda_c_finite).choose

lemma w_spec : S.c = Œª ^ S.multiplicity * S.w :=
(multiplicity.pow_multiplicity_dvd S.toSolution'.multiplicity_lambda_c_finite).choose_spec

lemma lambda_not_dvd_w : ¬¨ Œª ‚à£ S.w := by
  intro h
  replace h := mul_dvd_mul_left (Œª ^ S.multiplicity) h
  rw [‚Üê w_spec] at h
  have hh : _ := multiplicity.is_greatest' S.toSolution'.multiplicity_lambda_c_finite (lt_add_one S.multiplicity)
  rw [pow_succ', mul_comm] at hh
  exact hh h

lemma lambda_not_dvd_x : ¬¨ Œª ‚à£ S.x := by
  intro h
  replace h := mul_dvd_mul_left (Œª ^ (3 * S.multiplicity - 2)) h
  rw [mul_comm, ‚Üê x_spec] at h
  replace h := mul_dvd_mul (mul_dvd_mul h S.lambda_dvd_a_add_eta_mul_b) S.lambda_dvd_a_add_eta_sq_mul_b
  rw [‚Üê cube_add_cube_eq_mul, S.H, w_spec] at h
  simp only [Units.isUnit, IsUnit.dvd_mul_left] at h
  rw [‚Üê pow_succ', mul_comm, ‚Üê mul_assoc, ‚Üê pow_succ'] at h
  have := S.two_le_multiplicity
  have hh : 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S := by
    omega
  rw [hh, mul_pow, ‚Üê pow_mul, mul_comm _ 3, mul_dvd_mul_iff_left _] at h
  replace h := Prime.dvd_of_dvd_pow hŒ∂.lambda_prime h
  exact lambda_not_dvd_w _ h
  simp [lambda_ne_zero]

lemma coprime_x_y : IsCoprime S.x S.y := by
  apply isCoprime_of_prime_dvd
  ¬∑ simp only [not_and]
    intro _  hy
    apply lambda_not_dvd_y S
    simp [hy]
  ¬∑ intro p hp p_dvd_x p_dvd_y
    have aux1 := dvd_mul_of_dvd_right p_dvd_x (Œª ^ (3 * S.multiplicity - 2))
    rw [‚Üê x_spec] at aux1
    have aux2 := dvd_mul_of_dvd_right p_dvd_y (Œ∑ -1)
    rw [‚Üê y_spec] at aux2
    have aux3 : Associated p (Œ∑ -1) := by
      apply associated_of_dvd_a_add_b_of_dvd_a_add_eta_mul_b
      exact hp
      exact aux1
      exact aux2
    have aux4 : Œª ‚à£ S.x := by
      rw [‚Üê Associated.dvd_iff_dvd_left aux3]
      exact p_dvd_x
    exact lambda_not_dvd_x S aux4

lemma coprime_x_z : IsCoprime S.x S.z := by
  apply isCoprime_of_prime_dvd
  . simp only [not_and]
    intro _ hz
    apply lambda_not_dvd_z S
    simp [hz]
  . intro p hp p_dvd_x p_dvd_z
    have aux1 := dvd_mul_of_dvd_right p_dvd_x (Œª ^ (3 * S.multiplicity - 2))
    rw [‚Üê x_spec] at aux1
    have aux2 := dvd_mul_of_dvd_right p_dvd_z (Œ∑ - 1)
    rw [‚Üê z_spec] at aux2
    have aux3 : Associated p (Œ∑ -1) := by
      apply associated_of_dvd_a_add_b_of_dvd_a_add_eta_sq_mul_b
      exact hp
      exact aux1
      exact aux2
    have aux4 : Œª ‚à£ S.x := by
      rw [‚Üê Associated.dvd_iff_dvd_left aux3]
      exact p_dvd_x
    exact lambda_not_dvd_x S aux4

lemma coprime_y_z : IsCoprime S.y S.z := by
  apply isCoprime_of_prime_dvd
  . simp only [not_and]
    intro _ hz
    apply lambda_not_dvd_z S
    simp [hz]
  . intro p hp p_dvd_y p_dvd_z
    have aux1 := dvd_mul_of_dvd_right p_dvd_y (Œ∑ - 1)
    rw [‚Üê y_spec] at aux1
    have aux2 := dvd_mul_of_dvd_right p_dvd_z (Œ∑ - 1)
    rw [‚Üê z_spec] at aux2
    have aux3 : Associated p (Œ∑ -1) := by
      apply associated_of_dvd_a_add_eta_mul_b_of_dvd_a_add_eta_sq_mul_b
      exact hp
      exact aux1
      exact aux2
    have aux4 : Œª ‚à£ S.y := by
      rw [‚Üê Associated.dvd_iff_dvd_left aux3]
      exact p_dvd_y
    exact lambda_not_dvd_y S aux4

lemma mult_minus_two_plus_one_plus_one : 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S := by
  have this : 2 ‚â§ 3 * multiplicity S := by
    have := two_le_multiplicity S
    omega
  zify [this]
  ring

lemma x_mul_y_mul_z_eq_u_w_pow_three : S.x * S.y * S.z = S.u * S.w ^ 3 := by
  suffices hh : Œª ^ (3 * S.multiplicity - 2) * S.x * Œª * S.y * Œª * S.z = S.u * Œª ^ (3 * S.multiplicity) * S.w ^ 3 by
    rw [show Œª ^ (3 * multiplicity S - 2) * x S * Œª * y S * Œª * z S = Œª ^ (3 * multiplicity S - 2) * Œª * Œª * x S * y S * z S by ring] at hh
    rw [mul_comm _ (Œª ^ (3 * multiplicity S))] at hh
    simp only [‚Üê pow_succ] at hh
    have := S.two_le_multiplicity
    have hhh : 3 * multiplicity S - 2 + 1 + 1 = 3 * multiplicity S := by
      omega
    rw [hhh] at hh
    rw [mul_assoc, mul_assoc, mul_assoc] at hh
    simp [lambda_ne_zero] at hh
    convert hh using 1
    ring
  simp only [‚Üê x_spec, mul_assoc, ‚Üê y_spec, ‚Üê z_spec]
  simp only [mul_comm 3, pow_mul, ‚Üê mul_pow, ‚Üê w_spec]
  rw [‚Üê S.H, cube_add_cube_eq_mul]
  ring

lemma x_eq_unit_mul_cube : ‚àÉ (u‚ÇÅ : (ùìû K)À£) (X : ùìû K), S.x = u‚ÇÅ * X ^ 3 := by
  have h1 : S.x * (S.y * S.z * S.u‚Åª¬π) = S.w ^ 3 := by
    --simp only [x_mul_y_mul_z_eq_u_w_pow_three, ‚Üê mul_assoc] --this produces a timeout error
    simp only [‚Üê mul_assoc, x_mul_y_mul_z_eq_u_w_pow_three]
    simp only [mul_comm _ (S.w ^ 3), mul_assoc,mul_right_inv, Units.mul_inv, mul_one]
  have h2 : IsCoprime S.x (S.y * S.z * S.u‚Åª¬π) := by
    apply (isCoprime_mul_unit_right_right _ S.x _).mpr
    apply IsCoprime.mul_right S.coprime_x_y S.coprime_x_z
    simp only [Units.isUnit]
  have h3 : _ := exists_associated_pow_of_mul_eq_pow' h2 h1
  rcases h3 with ‚ü®X, ‚ü®u‚ÇÅ, hX‚ü©‚ü©
  use u‚ÇÅ; use X
  simp [‚Üê hX, mul_comm]

lemma y_eq_unit_mul_cube : ‚àÉ (u‚ÇÇ : (ùìû K)À£) (Y : ùìû K), S.y = u‚ÇÇ * Y ^ 3 := by
  have h1 : S.y * (S.x * S.z * S.u‚Åª¬π) = S.w ^ 3 := by
    rw [‚Üê mul_assoc, ‚Üê mul_assoc S.y, mul_comm S.y, x_mul_y_mul_z_eq_u_w_pow_three]
    simp only [mul_comm _ (S.w ^ 3), mul_assoc, mul_right_inv, Units.mul_inv, mul_one]
  have h2 : IsCoprime S.y (S.x * S.z * S.u‚Åª¬π) := by
    apply (isCoprime_mul_unit_right_right _ S.y _).mpr
    apply IsCoprime.mul_right S.coprime_x_y.symm S.coprime_y_z
    simp only [Units.isUnit]
  have h3 : _ := exists_associated_pow_of_mul_eq_pow' h2 h1
  rcases h3 with ‚ü®Y, ‚ü®u‚ÇÇ, hY‚ü©‚ü©
  use u‚ÇÇ; use Y
  simp [‚Üê hY, mul_comm]

lemma z_eq_unit_mul_cube : ‚àÉ (u‚ÇÉ : (ùìû K)À£) (Z : ùìû K), S.z = u‚ÇÉ * Z ^ 3 := by
  have h1 : S.z * (S.x * S.y * S.u‚Åª¬π) = S.w ^ 3 := by
    rw [‚Üê mul_assoc, ‚Üê mul_assoc S.z, mul_comm S.z, mul_assoc S.x, mul_comm S.z, ‚Üê mul_assoc, x_mul_y_mul_z_eq_u_w_pow_three]
    simp only [mul_comm _ (S.w ^ 3), mul_assoc, mul_right_inv, Units.mul_inv, mul_one]
  have h2 : IsCoprime S.z (S.x * S.y * S.u‚Åª¬π) := by
    apply (isCoprime_mul_unit_right_right _ S.z _).mpr
    apply IsCoprime.mul_right S.coprime_x_z.symm S.coprime_y_z.symm
    simp only [Units.isUnit]
  have h3 : _ := exists_associated_pow_of_mul_eq_pow' h2 h1
  rcases h3 with ‚ü®Z, ‚ü®u‚ÇÉ, hZ‚ü©‚ü©
  use u‚ÇÉ; use Z
  simp [‚Üê hZ, mul_comm]

/-- Given `S : Solution`, we let `S.u‚ÇÅ` and `S.X` be the elements such that
`S.x = S.u‚ÇÅ * S.X ^ 3` -/
noncomputable
def u‚ÇÅ := (x_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÅ` and `S.X` be the elements such that
`S.x = S.u‚ÇÅ * S.X ^ 3` -/
noncomputable
def X := (x_eq_unit_mul_cube S).choose_spec.choose

lemma u‚ÇÅ_X_spec : S.x = S.u‚ÇÅ * S.X ^ 3 := by
  exact (x_eq_unit_mul_cube S).choose_spec.choose_spec

/-- Given `S : Solution`, we let `S.u‚ÇÇ` and `S.Y` be the elements such that
`S.y = S.u‚ÇÇ * S.Y ^ 3` -/
noncomputable
def u‚ÇÇ := (y_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÇ` and `S.Y` be the elements such that
`S.y = S.u‚ÇÇ * S.Y ^ 3` -/
noncomputable
def Y := (y_eq_unit_mul_cube S).choose_spec.choose

lemma u‚ÇÇ_Y_spec : S.y = S.u‚ÇÇ * S.Y ^ 3 := by
  exact (y_eq_unit_mul_cube S).choose_spec.choose_spec

/-- Given `S : Solution`, we let `S.u‚ÇÉ` and `S.Z` be the elements such that
`S.z = S.u‚ÇÉ * S.Z ^ 3` -/
noncomputable
def u‚ÇÉ := (z_eq_unit_mul_cube S).choose

/-- Given `S : Solution`, we let `S.u‚ÇÉ` and `S.Z` be the elements such that
`S.z = S.u‚ÇÉ * S.Z ^ 3` -/
noncomputable
def Z := (z_eq_unit_mul_cube S).choose_spec.choose

lemma u‚ÇÉ_Z_spec : S.z = S.u‚ÇÉ * S.Z ^ 3 := by
  exact (z_eq_unit_mul_cube S).choose_spec.choose_spec

lemma X_ne_zero : S.X ‚â† 0 := by
  intro h
  have aux1 : S.x = 0 := by
    rw [u‚ÇÅ_X_spec, h]
    ring
  have aux2 : Œª ‚à£ S.x := by simp [aux1]
  apply lambda_not_dvd_x S
  exact aux2

lemma lambda_not_dvd_X : ¬¨ Œª ‚à£ S.X := by
  intro h
  have hyp := dvd_mul_of_dvd_right h (S.u‚ÇÅ * S.X ^ 2)
  rw [show ‚Üë(u‚ÇÅ S) * X S ^ 2 * X S = ‚ÜëS.u‚ÇÅ * S.X^3 by ring] at hyp
  rw [‚Üê u‚ÇÅ_X_spec] at hyp
  apply lambda_not_dvd_x S
  simp [hyp]

lemma lambda_not_dvd_Y : ¬¨ Œª ‚à£ S.Y := by
  intro h
  have hyp := dvd_mul_of_dvd_right h (S.u‚ÇÇ * S.Y^2)
  rw [show ‚Üë(u‚ÇÇ S) * Y S ^ 2 * Y S = ‚ÜëS.u‚ÇÇ * S.Y^3 by ring] at hyp
  rw [‚Üê u‚ÇÇ_Y_spec] at hyp
  apply lambda_not_dvd_y S
  simp [hyp]

lemma lambda_not_dvd_Z : ¬¨ Œª ‚à£ S.Z := by
  intro h
  have hyp := dvd_mul_of_dvd_right h (S.u‚ÇÉ * S.Z^2)
  rw [show ‚Üë(u‚ÇÉ S) * Z S ^ 2 * Z S = ‚ÜëS.u‚ÇÉ * S.Z^3 by ring] at hyp
  rw [‚Üê u‚ÇÉ_Z_spec] at hyp
  apply lambda_not_dvd_z S
  simp [hyp]

lemma coprime_Y_Z : IsCoprime S.Y S.Z := by
  apply isCoprime_of_prime_dvd
  ¬∑ simp only [not_and]
    intro _ hy_Z_zero
    apply lambda_not_dvd_Z S
    simp only [hy_Z_zero, dvd_zero]
  ¬∑ intro p hp p_dvd_Y p_dvd_Z
    have auxY := dvd_mul_of_dvd_right p_dvd_Y (S.u‚ÇÇ * S.Y^2)
    rw [show S.u‚ÇÇ * S.Y^2 * S.Y = S.u‚ÇÇ * S.Y^3 by ring] at auxY
    rw [‚Üê u‚ÇÇ_Y_spec] at auxY
    have auxZ := dvd_mul_of_dvd_right p_dvd_Z (S.u‚ÇÉ * S.Z^2)
    rw [show S.u‚ÇÉ * S.Z^2 * S.Z = S.u‚ÇÉ * S.Z^3 by ring] at auxZ
    rw [‚Üê u‚ÇÉ_Z_spec] at auxZ
    have gcd_isUnit : IsUnit (gcd S.y S.z) := by
      rw [gcd_isUnit_iff S.y S.z]
      simp only [coprime_y_z]
    apply hp.not_unit
    refine isUnit_of_dvd_unit ?_ gcd_isUnit
    rw [dvd_gcd_iff]
    simp [auxY, auxZ]

lemma formula1 : S.u‚ÇÅ*S.X^3*Œª^(3*S.multiplicity-2)+S.u‚ÇÇ*Œ∑*S.Y^3*Œª+S.u‚ÇÉ*Œ∑^2*S.Z^3*Œª = 0 := by
  rw [‚Üê u‚ÇÅ_X_spec, ‚Üê mul_comm Œ∑ _, mul_assoc Œ∑ _ _, ‚Üê u‚ÇÇ_Y_spec, ‚Üê mul_comm (Œ∑ ^ 2) _, mul_assoc (Œ∑ ^ 2) _ _, ‚Üê u‚ÇÉ_Z_spec]
  rw [mul_comm, mul_assoc, ‚Üê x_spec]
  rw [mul_comm, mul_comm _ Œª, ‚Üê y_spec, mul_comm _ Œ∑]
  rw [mul_assoc, mul_comm _ Œª, ‚Üê z_spec]
  rw [show S.a + S.b + Œ∑ * (S.a + Œ∑ * S.b) + Œ∑ ^ 2 * (S.a + Œ∑ ^ 2 * S.b) = S.a * (1 + Œ∑ + Œ∑ ^ 2) + S.b * (1 + (Œ∑ ^ 3) * Œ∑ + Œ∑ ^ 2) by ring]
  rw [hŒ∂.toInteger_cube_eq_one, one_mul, ‚Üê add_mul]
  convert mul_zero _
  convert hŒ∂.toInteger_eval_cyclo using 1
  ring

noncomputable
def u‚ÇÑ' := Œ∑ * S.u‚ÇÉ * S.u‚ÇÇ‚Åª¬π

lemma u‚ÇÑ'_isUnit : IsUnit S.u‚ÇÑ' := by
  unfold u‚ÇÑ'
  simp only [Units.isUnit_mul_units]
  exact hŒ∂.eta_isUnit

noncomputable
def u‚ÇÑ := (u‚ÇÑ'_isUnit S).unit

noncomputable
def u‚ÇÖ' := -Œ∑ ^ 2 * S.u‚ÇÅ * S.u‚ÇÇ‚Åª¬π

lemma u‚ÇÖ'_isUnit : IsUnit S.u‚ÇÖ' := by
  unfold u‚ÇÖ'
  rw [IsUnit.mul_iff, IsUnit.mul_iff]
  have minus_eta_sq_is_unit : IsUnit (- Œ∑ ^ 2) := by
    apply isUnit_iff_exists_inv.2
    use (-Œ∑)
    ring_nf
    exact hŒ∂.toInteger_cube_eq_one
  constructor
  ¬∑ constructor
    ¬∑ exact minus_eta_sq_is_unit
    ¬∑ simp only [Units.isUnit]
  ¬∑ simp only [Units.isUnit]

noncomputable
def u‚ÇÖ := (u‚ÇÖ'_isUnit S).unit

lemma formula2 : S.Y ^ 3 + S.u‚ÇÑ * S.Z ^ 3 = S.u‚ÇÖ * (Œª ^ (S.multiplicity - 1) * S.X) ^ 3 := by
  simp_rw [u‚ÇÑ, u‚ÇÖ, IsUnit.unit_spec]
  unfold u‚ÇÑ'
  unfold u‚ÇÖ'
  apply mul_left_cancel‚ÇÄ S.u‚ÇÇ.isUnit.ne_zero
  apply mul_left_cancel‚ÇÄ hŒ∂.eta_isUnit.ne_zero
  apply mul_left_cancel‚ÇÄ (lambda_ne_zero hŒ∂)
  rw [show Œª * (Œ∑ * (‚Üë(u‚ÇÇ S) * (Y S ^ 3 + Œ∑ * ‚Üë(u‚ÇÉ S) * ‚Üë(u‚ÇÇ S)‚Åª¬π * Z S ^ 3)))
    = Œª * Œ∑ * ‚Üë(u‚ÇÇ S) * Y S ^ 3 + Œª * Œ∑^2 * ‚Üë(u‚ÇÇ S) * ‚Üë(u‚ÇÇ S)‚Åª¬π * ‚Üë(u‚ÇÉ S) * Z S ^ 3 by ring]
  rw [show Œª * (Œ∑ * (‚Üë(u‚ÇÇ S) * (-Œ∑ ^ 2 * ‚Üë(u‚ÇÅ S) * ‚Üë(u‚ÇÇ S)‚Åª¬π
    * (Œª ^ (S.multiplicity - 1) * X S) ^ 3)))
    = Œª * (‚Üë(u‚ÇÇ S) * ‚Üë(u‚ÇÇ S)‚Åª¬π * (-Œ∑ ^ 3 * ‚Üë(u‚ÇÅ S) * (Œª ^ (S.multiplicity - 1) * X S) ^ 3)) by ring]
  rw [‚Üê sub_eq_zero]
  simp only [Units.mul_inv_cancel_right, Units.mul_inv, neg_mul, mul_neg, one_mul, sub_neg_eq_add]
  rw [hŒ∂.toInteger_cube_eq_one, one_mul]
  have tmp : Œª * (‚Üë(u‚ÇÅ S) * (Œª ^ (S.multiplicity - 1) * X S) ^ 3)
      = ‚Üë(u‚ÇÅ S) * X S ^ 3 * Œª ^ (3 * S.multiplicity - 2) := by
    rw [mul_comm, mul_assoc, mul_assoc]
    congr 1
    rw [mul_pow, mul_comm, ‚Üê mul_assoc, mul_comm _ (S.X ^ _)]
    congr 1
    rw [‚Üê pow_mul', ‚Üê pow_succ']
    congr 1
    have := two_le_multiplicity S
    omega
  rw [tmp]
  convert formula1 S using 1
  ring

variable (hŒ∂) in
lemma lambda_sq_div_lambda_fourth : Œª^2 ‚à£ Œª^4 := by
  use Œª^2
  ring

lemma lambda_sq_div_new_X_cubed : Œª^2 ‚à£ ‚Üë(u‚ÇÖ S) * (Œª ^ (multiplicity S - 1) * X S) ^ 3 := by
    have := two_le_multiplicity S
    have tmp : ‚Üë(u‚ÇÖ S) * Œª ^ (3 * S.multiplicity - 5) * Œª^2 * S.X^3
              =
              ‚Üë(u‚ÇÖ S) * (Œª ^ (multiplicity S - 1) * X S) ^ 3 := by
      rw [mul_comm, mul_assoc, mul_pow, ‚Üê mul_assoc _ _ (S.X ^ 3), mul_comm _ (S.X ^ 3)]
      congr 2
      rw [‚Üê pow_mul, ‚Üê pow_add]
      congr 1
      omega
    rw [‚Üê tmp]
    use S.u‚ÇÖ * (Œª ^ (3* S.multiplicity - 5) * X S^ 3)
    ring

variable [DecidableEq (ùìû K)]

lemma by_kummer : ‚ÜëS.u‚ÇÑ ‚àà ({1, -1} : Finset (ùìû K)) := by
  have h0 := lambda_sq_div_lambda_fourth hŒ∂
  have hX := lambda_sq_div_new_X_cubed S
  suffices hh : S.u‚ÇÑ = 1 ‚à® S.u‚ÇÑ = -1 by
    rcases hh with (h | h) <;> simp [h]
  apply eq_one_or_neg_one_of_unit_of_congruent hŒ∂
  rcases hX with ‚ü®kX, hkX‚ü©
  rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd hŒ∂ S.lambda_not_dvd_Y with
    (HY | HY) <;> rcases lambda_pow_four_dvd_cube_sub_one_or_add_one_of_lambda_not_dvd
      hŒ∂ S.lambda_not_dvd_Z with (HZ | HZ) <;> replace HY := h0.trans HY <;> replace HZ :=
      h0.trans HZ <;> rcases HY with ‚ü®kY, hkY‚ü© <;> rcases HZ with ‚ü®kZ, hkZ‚ü©
  ¬∑ use -1
    use kX - kY - S.u‚ÇÑ * kZ
    -- SLIDE
    rw [show Œª ^ 2 * (kX - kY - ‚Üë(u‚ÇÑ S) * kZ) = Œª ^ 2 * kX - Œª ^ 2 * kY - ‚Üë(u‚ÇÑ S) * (Œª ^ 2 * kZ) by ring]
    rw [‚Üê hkX, ‚Üê hkY, ‚Üê hkZ]
    rw [‚Üê S.formula2]
    ring
  ¬∑ use 1
    use - kX + kY + S.u‚ÇÑ * kZ
    rw [show Œª ^ 2 * (-kX + kY + ‚Üë(u‚ÇÑ S) * kZ) = - (Œª ^ 2 * kX - Œª ^ 2 * kY - ‚Üë(u‚ÇÑ S) * (Œª ^ 2 * kZ)) by ring]
    rw [‚Üê hkX, ‚Üê hkY, ‚Üê hkZ, ‚Üê S.formula2]
    ring
  ¬∑ use 1
    use kX - kY - S.u‚ÇÑ * kZ
    rw [show Œª ^ 2 * (kX - kY - ‚Üë(u‚ÇÑ S) * kZ) = Œª ^ 2 * kX - Œª ^ 2 * kY - ‚Üë(u‚ÇÑ S) * (Œª ^ 2 * kZ) by ring]
    rw [‚Üê hkX, ‚Üê hkY, ‚Üê hkZ, ‚Üê S.formula2]
    ring
  ¬∑ use -1
    use - kX + kY + S.u‚ÇÑ * kZ
    rw [show Œª ^ 2 * (-kX + kY + ‚Üë(u‚ÇÑ S) * kZ) = - (Œª ^ 2 * kX - Œª ^ 2 * kY - ‚Üë(u‚ÇÑ S) * (Œª ^ 2 * kZ)) by ring]
    rw [‚Üê hkX, ‚Üê hkY, ‚Üê hkZ, ‚Üê S.formula2]
    ring

/-- Given `S : Solution`, we have that
`S.Y ^ 3 + (S.u‚ÇÑ * S.Z) ^ 3 = S.u‚ÇÖ * (Œª ^ (S.multiplicity - 1) * S.X) ^ 3`. -/
lemma final : S.Y ^ 3 + (S.u‚ÇÑ * S.Z) ^ 3 = S.u‚ÇÖ * (Œª ^ (S.multiplicity - 1) * S.X) ^ 3 := by
  rw [show S.Y ^ 3 + (S.u‚ÇÑ * S.Z) ^ 3 = S.Y ^ 3 + S.u‚ÇÑ^2 * S.u‚ÇÑ * S.Z ^ 3 by ring]
  have f2 := formula2 S
  rw [show Y S ^ 3 + ‚Üë(u‚ÇÑ S) * Z S ^ 3 = Y S ^ 3 + 1 * ‚Üë(u‚ÇÑ S) * Z S ^ 3 by ring] at f2
  have simple_kummer := by_kummer S
  simp only [Finset.mem_insert, Finset.mem_singleton] at simple_kummer
  have hh : S.u‚ÇÑ ^ 2 = (1 : ùìû K) := by
    rcases simple_kummer with (h | h) <;> simp only [h, one_pow, even_two, Even.neg_pow, one_pow]
  nth_rewrite 1 [‚Üê hh] at f2
  exact f2

noncomputable
def _root_.Solution'_final : Solution' hŒ∂ where
  a := S.Y
  b := S.u‚ÇÑ * S.Z
  c := Œª ^ (S.multiplicity - 1) * S.X
  u := S.u‚ÇÖ
  ha := S.lambda_not_dvd_Y
  hb := fun h ‚Ü¶ S.lambda_not_dvd_Z <| Units.dvd_mul_left.1 h
  hc := fun h ‚Ü¶ S.X_ne_zero <| by simpa [lambda_ne_zero] using h
  coprime := (isCoprime_mul_unit_left_right S.u‚ÇÑ.isUnit _ _).2 S.coprime_Y_Z
  hcdvd := by
    refine dvd_mul_of_dvd_left (dvd_pow_self _ (fun h ‚Ü¶ ?_)) _
    rw [Nat.sub_eq_iff_eq_add (le_trans (by norm_num) S.two_le_multiplicity), zero_add] at h
    simpa [h] using S.two_le_multiplicity
  H := final S

lemma _root_.Solution'_final_multiplicity :
    (Solution'_final S).multiplicity = S.multiplicity - 1 := by
  refine (multiplicity.unique' (by simp [Solution'_final]) (fun h ‚Ü¶ S.lambda_not_dvd_X ?_)).symm
  obtain ‚ü®k, hk : Œª ^ (S.multiplicity - 1) * S.X = Œª ^ (S.multiplicity - 1 + 1) * k‚ü© := h
  rw [pow_succ, mul_assoc] at hk
  simp only [mul_eq_mul_left_iff, pow_eq_zero_iff', lambda_ne_zero, ne_eq, false_and,
    or_false] at hk
  simp [hk]

lemma _root_.Solution'_final_multiplicity_lt :
    (Solution'_final S).multiplicity < S.multiplicity := by
  rw [Solution'_final_multiplicity S, Nat.sub_one]
  exact Nat.pred_lt <| by have := S.two_le_multiplicity; omega

theorem exists_Solution_multiplicity_lt :
    ‚àÉ (S' : Solution hŒ∂), S'.multiplicity < S.multiplicity := by
  obtain ‚ü®S', hS'‚ü© := exists_Solution_of_Solution' (Solution'_final S)
  exact ‚ü®S', hS' ‚ñ∏ Solution'_final_multiplicity_lt S‚ü©

end Solution

end FermatLastTheoremForThreeGen

end eisenstein

end case2

theorem fermatLastTheoremThree : FermatLastTheoremFor 3 := by
  classical
  let K := CyclotomicField 3 ‚Ñö
  let hŒ∂ := IsCyclotomicExtension.zeta_spec 3 ‚Ñö K
  have : NumberField K := IsCyclotomicExtension.numberField {3} ‚Ñö _
  apply FermatLastTheoremForThree_of_FermatLastTheoremThreeGen hŒ∂
  intro a b c u hc ha hb hcdvd coprime H
  let S' : Solution' hŒ∂ :=
  { a := a
    b := b
    c := c
    u := u
    ha := ha
    hb := hb
    hc := hc
    coprime := coprime
    hcdvd := hcdvd
    H := H }
  obtain ‚ü®S, -‚ü© := exists_Solution_of_Solution' S'
  obtain ‚ü®Smin, hSmin‚ü© := S.exists_minimal
  obtain ‚ü®Sfin, hSfin‚ü© := Smin.exists_Solution_multiplicity_lt
  linarith [hSmin Sfin]
